class LayerListPanel {
  PApplet parent;
  Document doc;
  int rightPanelX;
  int panelWidth;
  int topY;
  boolean isRefreshing = false;

  DefaultListModel<Layer> model = new DefaultListModel<Layer>();
  JList<Layer> list = new JList<Layer>(model);
  JScrollPane scrollPane;
  ImageIcon add = new ImageIcon("data/icon/addlayer.png");
  ImageIcon remove = new ImageIcon("data/icon/trashbin.png");




  JButton addButton = new JButton(add);
  JButton removeButton = new JButton(remove);
  JPanel container = new JPanel(new BorderLayout(6, 6));

  final int EYE_W = 28;

  LayerListPanel(PApplet parent, Document doc, int rightPanelX, int panelWidth, int topY) {
    this.parent = parent;
    this.doc = doc;
    this.rightPanelX = rightPanelX;
    this.panelWidth = panelWidth;
    this.topY = topY;

    configureList();
    configureHeader();
    attachToFrame();
    refresh(doc);
  }


  void configureList() {
    list.setSelectionMode(ListSelectionModel.SINGLE_SELECTION);

    list.setCellRenderer(new LayerCellRenderer(EYE_W));

    list.setDragEnabled(true);
    list.setDropMode(DropMode.INSERT);
    list.setAutoscrolls(true);
    list.setTransferHandler(new ReorderHandler());
    list.setBorder(BorderFactory.createEmptyBorder(4, 6, 4, 6));

    list.setFixedCellHeight(56);                 // 48Áº©Áï•Âõæ + ‰∏ä‰∏ãÂëºÂê∏
    list.setBorder(BorderFactory.createEmptyBorder(6, 6, 6, 6));
    list.setOpaque(true);



    // select active layers
    list.addListSelectionListener(e -> {
      if (isRefreshing || e.getValueIsAdjusting()) return;
      int idx = list.getSelectedIndex();
      int docIdx = viewToDocIndex(idx);
      doc.layers.activeIndex = docIdx;
      if (app != null && app.ui != null) {
        app.ui.updatePropertiesFromLayer(doc.layers.getActive());
      }
    });
  

    // mouse: click eye to toggle; double click to rename
    list.addMouseListener(new MouseAdapter() {
      public void mousePressed(java.awt.event.MouseEvent e) {
        int idx = list.locationToIndex(e.getPoint());
        if (idx < 0) return;

        Rectangle cell = list.getCellBounds(idx, idx);
        int localX = e.getX() - cell.x;

        Layer layer = model.getElementAt(idx);

        // click eye area
        if (localX >= 0 && localX <= EYE_W) {
          app.history.perform(doc, new ToggleVisibleCommand(layer));
          list.repaint();
          return;
        }

        // double click to rename
        if (e.getClickCount() == 2) {
          String newName = JOptionPane.showInputDialog(container, "Rename layer:", layer.name);
          if (newName != null) {
            newName = newName.trim();
            if (newName.length() > 0) {
              app.history.perform(doc, new RenameLayerCommand(layer, newName));
              list.repaint();
            }
          }
        }
      }
    });


    // Delete: use Swing key binding (not processing.event.KeyEvent)
    list.addMouseListener(new java.awt.event.MouseAdapter() {
      public void mousePressed(java.awt.event.MouseEvent e) {
        list.requestFocusInWindow();
      }
    });




    InputMap im = list.getInputMap(JComponent.WHEN_FOCUSED);
    ActionMap am = list.getActionMap();
    int PRIMARY = Toolkit.getDefaultToolkit().getMenuShortcutKeyMaskEx(); // Ctrl on Win/Linux, Cmd on macOS
    im.put(KeyStroke.getKeyStroke(java.awt.event.KeyEvent.VK_DELETE, 0), "deleteLayer");
    im.put(KeyStroke.getKeyStroke(java.awt.event.KeyEvent.VK_BACK_SPACE, PRIMARY), "deleteLayer");
    im.put(KeyStroke.getKeyStroke(java.awt.event.KeyEvent.VK_T,0), "addTextLayer");
    am.put("deleteLayer", new AbstractAction() {
      public void actionPerformed(ActionEvent e) {
        deleteSelectedLayer();
      }
    });
    am.put("addTextLayer", new AbstractAction() {
      public void actionPerformed(ActionEvent e) {
        addTextLayer();
      }
    });

    scrollPane = new JScrollPane(list);
    scrollPane.setBorder(BorderFactory.createEmptyBorder());
    scrollPane.getViewport().setOpaque(true);
    scrollPane.setOpaque(true);


  }

  void configureHeader() {
    JPanel header = new JPanel(new BorderLayout(10, 10));
    header.setOpaque(true);
    header.setBorder(BorderFactory.createEmptyBorder(10, 10, 10, 10));
    JLabel label = new JLabel("Layers");
    label.setFont(new Font("SansSerif", Font.BOLD, 16));


    addButton.setMargin(new Insets(6, 6, 6, 6));
    removeButton.setMargin(new Insets(6, 6, 6, 6));

    addButton.addActionListener(e -> addBlankLayer());
    removeButton.addActionListener(e->deleteSelectedLayer());

    JPanel actions = new JPanel(new FlowLayout(FlowLayout.RIGHT, 6, 0));
    actions.setOpaque(true);
    actions.add(addButton);
    actions.add(removeButton);

    header.add(label, BorderLayout.WEST);
    header.add(actions, BorderLayout.EAST);

    container.setOpaque(true);
    container.add(header, BorderLayout.NORTH);
    container.add(scrollPane, BorderLayout.CENTER);
  }

  void attachToFrame() {
    PSurfaceAWT surf = (PSurfaceAWT) parent.getSurface();
    PSurfaceAWT.SmoothCanvas canvas = (PSurfaceAWT.SmoothCanvas) surf.getNative();
    JFrame frame = (JFrame) canvas.getFrame();
    frame.getLayeredPane().add(container, JLayeredPane.PALETTE_LAYER);
    frame.getLayeredPane().setLayout(null);
  }



  void updateLayout(int rightPanelX, int panelWidth, int parentHeight) {
    this.rightPanelX = rightPanelX;
    this.panelWidth = panelWidth;
    int margin = 0;
    int availableHeight = Math.max(120, parentHeight - topY);
    container.setBounds(rightPanelX + margin, topY, panelWidth - margin * 2, availableHeight);
    container.revalidate();
  }

  // rebuild the layer list from Document
  void refresh(Document updatedDoc) {
    this.doc = updatedDoc;
    isRefreshing = true;

    model.clear();
    ArrayList<Layer> layers = updatedDoc.layers.list;
    for (int i = layers.size() - 1; i >= 0; i--) model.addElement(layers.get(i));

    int viewIdx = docToViewIndex(updatedDoc.layers.activeIndex);
    if (viewIdx >= 0) list.setSelectedIndex(viewIdx);
    else list.clearSelection();

    list.repaint();
    isRefreshing = false;
  }


  boolean isFocusInside() {
    Component focus = KeyboardFocusManager.getCurrentKeyboardFocusManager().getFocusOwner();
    return focus != null && SwingUtilities.isDescendingFrom(focus, container);
  }



  void addBlankLayer() {
    PImage blank = parent.createImage(doc.canvas.width, doc.canvas.height, ARGB);
    blank.loadPixels();
    for (int i = 0; i < blank.pixels.length; i++) blank.pixels[i] = parent.color(0, 0, 0, 0);
    blank.updatePixels();
    Layer newLayer = new Layer(blank,doc.layers.getid());
    newLayer.empty = true;
    newLayer.name = "Layer " + (doc.layers.list.size() + 1);
    int index = doc.layers.list.size();
    app.history.perform(doc, new AddLayerCommand(newLayer, index));
    doc.layers.activeIndex = viewToDocIndex(index);
    doc.renderFlags.dirtyComposite = true;
    refresh(doc);
  }

  void addTextLayer() {
    int idx=doc.layers.indexOf(doc.layers.getActive())+1;
    TextLayer tl=new TextLayer("New Text","Arial",32,doc.layers.getid());
    tl.x = doc.viewW * 0.5 - tl.pivotX;
    tl.y = doc.viewH * 0.5 - tl.pivotY;
    app.history.perform(doc, new AddLayerCommand(tl, idx));
    doc.layers.activeIndex = viewToDocIndex(idx);
    doc.renderFlags.dirtyComposite = true;
    refresh(doc);
  }
  void deleteSelectedLayer() {
    Layer layer = list.getSelectedValue();
    if (layer == null) return;

    app.history.perform(doc, new RemoveLayerCommand(layer));
    doc.renderFlags.dirtyComposite = true;
    refresh(doc);
  }

  class ReorderHandler extends TransferHandler {
    int sourceIndex = -1;

    public int getSourceActions(JComponent c) { return MOVE; }

    protected Transferable createTransferable(JComponent c) {
      sourceIndex = list.getSelectedIndex();
      Layer layer = list.getSelectedValue();
      return new StringSelection(layer == null ? "" : ("" + layer.ID));
    }

    public boolean canImport(TransferSupport support) { return support.isDrop(); }

    public boolean importData(TransferSupport support) {
      if (!support.isDrop()) return false;

      JList.DropLocation dl = (JList.DropLocation) support.getDropLocation();
      int target = dl.getIndex();

      if (target < 0) target = model.getSize();
      target = Math.max(0, Math.min(target, model.getSize()));
      if (target > sourceIndex) target--;

      Layer layer = model.getElementAt(sourceIndex);

      int sourceDocIndex = viewToDocIndex(sourceIndex);
      int targetDocIndex = viewToDocIndex(target);
      if (sourceDocIndex < 0 || targetDocIndex < 0) return false;

      app.history.perform(doc, new MoveLayerCommand(layer, sourceDocIndex, targetDocIndex));

      refresh(doc);
      int viewIdx = docToViewIndex(doc.layers.indexOf(layer));
      if (viewIdx >= 0) list.setSelectedIndex(viewIdx);
      return true;
    }
  }



  // ---------- Renderer ----------
  class LayerCellRenderer extends JPanel implements ListCellRenderer<Layer> {
    int eyeW;
    JLabel eyeLabel = new JLabel("", SwingConstants.CENTER);
    JLabel nameLabel = new JLabel("");
    JLabel iconLabel = new JLabel();
    static final int ICON_W = 48;
    static final int ICON_H = 48;

    LayerCellRenderer(int eyeW) {
      this.eyeW = eyeW;
      setLayout(new BorderLayout());
      setBorder(BorderFactory.createEmptyBorder(2, 2, 2, 2));
      setOpaque(true);
      
      eyeLabel.setPreferredSize(new Dimension(eyeW, 22));
      eyeLabel.setForeground(Color.WHITE);

      nameLabel.setForeground(Color.WHITE);
      nameLabel.setBorder(BorderFactory.createEmptyBorder(0, 6, 0, 0));

      iconLabel.setPreferredSize(new Dimension(ICON_W, ICON_H));
      iconLabel.setBorder(BorderFactory.createEmptyBorder(0, 0, 0, 0));
      iconLabel.setHorizontalAlignment(SwingConstants.CENTER);

      add(eyeLabel, BorderLayout.WEST);
      add(nameLabel, BorderLayout.CENTER);
      add(iconLabel, BorderLayout.EAST);
    }
  private ImageIcon makeIconFromLayer(Layer layer) {
    if (layer == null) return null;
    PImage thumb = layer.getThumbnail();
    if (thumb == null) return null;
    java.awt.Image img = (java.awt.Image) thumb.getNative();
    java.awt.Image scaled = img.getScaledInstance(ICON_W, ICON_H, java.awt.Image.SCALE_SMOOTH);
    return new ImageIcon(scaled);
  }
    public Component getListCellRendererComponent(
      JList<? extends Layer> list, 
      Layer layer, 
      int index,
      boolean isSelected, 
      boolean cellHasFocus
    )
    {
        eyeLabel.setText(layer.visible ? "üëÅ" : "‚¶∏");
    ImageIcon icon = makeIconFromLayer(layer);
      iconLabel.setIcon(icon);
      nameLabel.setText(layer.name);
      Color bg = isSelected ? new Color(80, 80, 80) : list.getBackground();
      setBackground(bg);

      return this;
    }
  }
  


  int docToViewIndex(int docIndex) {
    int size = doc.layers.list.size();
    if (docIndex < 0 || docIndex >= size) return -1;
    return size - 1 - docIndex;
  }

  int viewToDocIndex(int viewIndex) {
    int size = doc.layers.list.size();
    if (viewIndex < 0 || viewIndex >= size) return -1;
    return size - 1 - viewIndex;
  }
}
